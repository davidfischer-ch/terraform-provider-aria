---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "aria_policy Resource - aria"
subcategory: ""
description: |-
  Policy resource
---

# aria_policy (Resource)

Policy resource

## Example Usage

```terraform
# main.tf

resource "aria_policy" "snapshot_revert_approval" {
  name             = "Snapshot Revert Approval Policy"
  description      = "Require some user to approve a snapshot's revert operation."
  enforcement_type = "HARD"
  type_id          = "com.vmware.policy.approval"

  scope_criteria = jsonencode({
    matchExpression = [
      {
        or = [
          {
            key      = "project.name"
            operator = "contains"
            value    = "REC"
          },
          {
            key      = "project.name"
            operator = "contains"
            value    = "PROD"
          }
        ]
      }
    ]
  })

  definition = jsonencode({
    level                = 1
    actions              = ["Cloud.vSphere.Machine.Snapshot.Revert"]
    approvers            = ["USER:SOMEUSER"]
    approvalMode         = "ANY_OF"
    approverType         = "USER"
    autoApprovalExpiry   = 7
    autoApprovalDecision = "REJECT"
  })
}

locals {
  redis_custom_resource = {
    type = "Redis_v1.0"
  }

  redis_template = {
    catalog_item = {
      id = "c7e95ef0-7608-4b09-b11b-87a80b9347aa"
    }
  }
}

resource "aria_policy" "redis_day2" {
  name             = "${local.redis_custom_resource.type} - Day 2 Policy"
  description      = "Restrict access to Redis Day 2 actions to a restricted set of users"
  enforcement_type = "HARD"
  type_id          = "com.vmware.policy.deployment.action"

  definition = jsonencode({
    allowedActions = [
      {
        actions = [
          "Deployment.Update",
          "Deployment.Delete",
          "${local.redis_custom_resource.type}.custom.snapshot",
          "${local.redis_custom_resource.type}.custom.restore",
          "${local.redis_custom_resource.type}.custom.getresourcestatus",
        ]
        authorities = [
          "GROUP:<redacted>.CMP.CMP-ARIA-AUT-ALL-ADMINISTRATOR.DEV@<redacted>",
          "GROUP:<redacted>.CMP.CMP-ARIA-AUT-ALL-INT-OPERATOR.DEV@<redacted>",
          "GROUP:<redacted>.CMP.CMP-ARIA-AUT-ALL-EXPL-OPERATOR.DEV@<redacted>"
        ]
      }
    ]
  })

  scope_criteria = jsonencode({
    matchExpression = [
      {
        or = [
          for project_name in ["LAB", "DEV", "REC", "PROD"] :
          {
            key      = "project.name"
            operator = "eq"
            value    = project_name
          }
        ]
      }
    ]
  })

  criteria = jsonencode({
    matchExpression = [
      {
        key      = "catalogItemId"
        operator = "eq"
        value    = local.redis_template.catalog_item.id
      }
    ]
  })
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `definition` (String) Definition (JSON encoded)

We should have implemented this attribute as a dynamic type (and not JSON).
Unfortunately Terraform SDK returns this issue:
Dynamic types inside of collections are not currently supported in terraform-plugin-framework.
- `description` (String) Describe the resource in few sentences
- `enforcement_type` (String) Enforcement type, either `SOFT` or `HARD` (force recreation on change)
- `name` (String) Policy name
- `type_id` (String) Policy type (force recreation on change), one of `com.vmware.policy.approval`, `com.vmware.policy.catalog.entitlement`, `com.vmware.policy.deployment.action`, `com.vmware.policy.deployment.lease`, `com.vmware.policy.deployment.limit` or `com.vmware.policy.resource.quota`

### Optional

- `criteria` (String) Filtering criteria (JSON encoded)

We should have implemented this attribute as a dynamic type (and not JSON).
Unfortunately Terraform SDK returns this issue:
Dynamic types inside of collections are not currently supported in terraform-plugin-framework.
- `project_id` (String) Project identifier. Empty or unset means available for all projects. (force recreation on change)
- `scope_criteria` (String) Scoping criteria (force recreation on change) (JSON encoded)

We should have implemented this attribute as a dynamic type (and not JSON).
Unfortunately Terraform SDK returns this issue:
Dynamic types inside of collections are not currently supported in terraform-plugin-framework.

### Read-Only

- `created_at` (String) Creation timestamp (RFC3339)
- `created_by` (String) User who created the resource
- `id` (String) Identifier
- `last_updated_at` (String) Last update timestamp (RFC3339)
- `last_updated_by` (String) Last user who updated the resource
- `org_id` (String) Organization identifier

## Import

Import is supported using the following syntax:

```shell
# Policy can be imported by specifying the instance's unique identifier.
terraform import aria_policy.example 9ea6205b-e0e1-4188-b275-b17299efe49a
```
